;t1irgp1d_jy
;
;download from: TBD
;
;T1 measurement using inversion recovery 
;using broadband 1H inversion pulse and field gradients
;1D version with only one increment
;set inversion-recovery delay to d27
;
;Tim Claridge & Jonathan Yong - University of Oxford - 20 June 2021
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"p2=p1*2"
"d11=30m"


"acqt0=-p1*2/3.1416"


1 ze
2 50m BLKGRAD
  d1 
  "TAU=(d27-p19-d16-4u)"
  if "TAU > 0.96"
  {
  "DELTA=TAU-0.45"
  }
  50u UNBLKGRAD
  4u pl0:f1
  (p44:sp30 ph1):f1
  p19:gp1 
  d16 
  if "TAU > 0.96"
  { 
  0.4s 
  50m BLKGRAD
  DELTA pl1:f1
  }
  else
  {
  TAU pl1:f1
  } 
  p1 ph2
  go=2 ph31
  50m mc #0 to 2 F0(zd)
50m BLKGRAD
exit


ph1=0 2 
ph2=0 0 2 2 1 1 3 3
ph31=0 0 2 2 1 1 3 3


;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;vd : variable delay, taken from vd-list
;ns: 1 * n
;ds: 0
;td1: number of experiments = number of delays in vd-list
;FnMODE: undefined
;d27: inversion-recovery delay
