;noesygppr1d_jy
;
;download from: https://git.io/Jnokc
;
;modified from noesygppr1d to use CNST20 as presaturation RF amplitude (in Hz)
;Jonathan Yong - University of Oxford - 20 June 2021
;
;$CLASS=HighRes
;$DIM=1D
;$TYPE=
;$SUBTYPE=
;$COMMENT=
;$RECOMMEND=

#include <Avance.incl>
#include <Grad.incl>

"d12=20u"
"plw9=plw1*(cnst20/(250000/p1))*(cnst20/(250000/p1))"
"acqt0=-p0*2/3.1416"


1 ze
2 30m

#   ifdef FLAG_BLK
  4u BLKGRAD
#   else
  4u
#   endif /*FLAG_BLK*/

  d12 pl9:f1
  d1 cw:f1 ph29
  4u do:f1
  50u UNBLKGRAD
  p16:gp1
  d16 pl1:f1
  p1 ph1
  4u
  p1 ph2
  d12 pl9:f1
  d8 cw:f1
  4u do:f1
  p16:gp2
  d16 pl1:f1

#   ifdef FLAG_BLK
  4u
#   else
  4u BLKGRAD
#   endif /*FLAG_BLK*/

  p0 ph3
  go=2 ph31
  30m mc #0 to 2 F0(zd)

#   ifdef FLAG_BLK
  4u BLKGRAD
#   else
  4u
#   endif /*FLAG_BLK*/

exit


ph1=0 2 
ph2=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph3=0 0 2 2 1 1 3 3
ph29=0
ph31=0 2 2 0 1 3 3 1 2 0 0 2 3 1 1 3


;pl1 : f1 channel - power level for pulse (default)
;pl9 : f1 channel - power level for presaturation
;p0 : for any flip angle
;p1 : f1 channel -  90 degree high power pulse
;p16: homospoil/gradient pulse
;d1 : relaxation delay; 1-5 * T1
;d8 : mixing time
;d12: delay for power switching                      [20 usec]
;d16: delay for homospoil/gradient recovery
;ns: 8 * n, total number of scans: NS * TD0
;ds: 4
;cnst20: presaturation power (Hz)


;use gradient ratio:    gp 1 : gp 2
;                         50 :  -10

;for z-only gradients:
;gpz1: 50%
;gpz2: -10%

;use gradient files:
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100



                                          ;preprocessor-flags-start
;FLAG_BLK: for BLKGRAD before d1 rather than go
;             option -DFLAG_BLK: (eda: ZGOPTNS)
                                          ;preprocessor-flags-end

