;psyche_1dopt_jy
;
;download from: https://git.io/JUHOS
;
;90 - 15m delay - double saltire - 15m delay - acquire
;Jonathan Yong - University of Oxford - 26 Aug 2020

;Optimisation parameters:
;cnst20 : flip angle (degrees)
;gpz10  : gradient amplitude (%)
;cnst21 : bandwidth (Hz)
;p40    : saltire + gradient duration (us)

#include <Avance.incl>
#include <Grad.incl>

"d2=15m"
"cnst10=(cnst20/360)*sqrt((2*cnst21)/(p40/2000000))" ; rf amplitude of saltire
"cnst11=1000000/(p1*4)"                              ; rf amplitude hard pulse
"spw40=plw1*(cnst10/cnst11)*(cnst10/cnst11)"         ; power level of saltire

"acqt0=0"
baseopt_echo

1 ze
2 30m
  50u BLKGRAD
  d1 pl1:f1
  50u UNBLKGRAD

  p1 ph1

  d2
  d16
  p16:gp1
  d16

  (center (p40:sp40 ph3):f1 (p40:gp10) )

  d16
  p16:gp1
  d16
  d2
  
  go=2 ph31
  30m mc #0 to 2 F0(zd)

50u BLKGRAD
exit

ph1 = 0
ph3 = 0 1 2 3
ph31= 0 2 0 2


;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  high power pulse
;p40: shaped pulse in PS element
;p21: gradient during PS element
;d1 : relaxation delay; 1-5 * T1
;d2 : delay on either side of double saltire
;d16: gradient recovery delay
;p16: CTP gradient duration
;ns: 1 * n, total number of scans: NS * TD0

;cnst20: flip angle (degrees)
;cnst21: bandwidth of PSYCHE element (Hz) (ca. 10000)

;gpnam10: RECT.1
;gpz10  : 2%
;gpnam1 : SINE.100
;gpz1   : 50%
